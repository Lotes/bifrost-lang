grammar Bifrost

entry File: 
    (
        implementations+=NodeTypeDefinition
        | dataTypeDefinitions+=DataTypeDefinition
    )*
    ;

type SignatureType = DataTypeDefinition | NodeTypeDefinition;

fragment TypeParametersClause: '<' typeParameters+=TypeParameter (',' typeParameters+=TypeParameter)* '>';
fragment TypeParameterAssignmentsClause: '<' arguments+=TypeExpression (',' arguments+=TypeExpression)* '>';

DataTypeDefinition:
    'datatype' name=UPPER_ID TypeParametersClause? '{'
        constructors+=DataTypeConstructorDefinition (',' constructors+=DataTypeConstructorDefinition)*
    '}'
    ;

DataTypeConstructorDefinition:
    name=UPPER_ID TypeParametersClause?
    ;

NodeTypeDefinition:
    abstract?='abstract'?
    'nodetype' name=UPPER_ID TypeParametersClause? 
    ('of' iface=[NodeTypeDefinition] TypeParameterAssignmentsClause?)?
    '(' (ports+=PortDefinition (',' ports+=PortDefinition)*)? ')' 
    '{' body=NodeTypeBody '}'
    ;

NodeTypeBody:
    (
        nodes+=Node ';'
        | links+=LinkDefinition ';'
        | matches+=PatternMatching
    )*
    ;

TypeParameter: name=LOWER_ID;

TypeExpression:
    {infer TypeParameterReference} typeParameter=[TypeParameter]
    | {infer TypeApplication} dataType=[SignatureType] TypeParameterAssignmentsClause?
    | {infer ParenthesesTypeExpression} '(' type=TypeExpression ')'
    ;


PatternMatching:
    'match' source=Expression 'with' '{' 
        (matches+=PatternMatchDefinition)*
    '}'
    ;

PatternMatchDefinition:
    'case' pattern=Expression '{'
        body=NodeTypeBody
    '}';

LinkDefinition: 'link' source=Expression 'to' target=Expression;

Expression:
    Addition
    ({infer BinaryWireTargetExpression.left=current} op=('=='|'!='|'<'|'>'|'<='|'>=') right=Addition)*
    ;
Addition:
    Factor 
    ({infer BinaryWireTargetExpression.left=current} op=('+'|'-') right=Factor)*
    ;

Factor:
    Primary 
    ({infer BinaryWireTargetExpression.left=current} op=('*'|'/'|'mod'|'div') right=Primary)*
    ;

Primary:    
     {infer SelfPortExpression} 'this' '.' portRef=[PortDefinition:LOWER_ID]
     | {infer NodePortExpression} 'this' '.' instanceRef=[Node] '.' portRef=[PortDefinition:LOWER_ID]
     | {infer NumericLiteral} number=INT
     | {infer StringLiteral} string=STRING
     | {infer ParenthesesExpression} '(' expression=Expression ')'
     | {infer ConstructorApplication} constructor=[DataTypeConstructorDefinition] arguments+=Primary*
     | {infer MatchVariableDefinition} name=LOWER_ID '?'
     | {infer MatchVariableUsage} variable=[MatchVariableDefinition:LOWER_ID]
     ;

Node:
    'node' name=LOWER_ID 'of' source=InstanceSource
    ;

InstanceSource:
    {infer NodeTypeDefinitionSource} nodeDef=[NodeTypeDefinition] TypeParameterAssignmentsClause?
    | {infer SelfPortSource} 'this' '.' portRef=[PortDefinition:LOWER_ID]
    | {infer NodePortSource} 'this' '.' instanceRef=[Node] '.' portRef=[PortDefinition:LOWER_ID]
    ;

PortDefinition: direction=Direction name=LOWER_ID 'of' type=DataType;

DataType:
    {infer TypeConstructor} constructor=[DataTypeConstructorDefinition] TypeParameterAssignmentsClause?
    | {infer IntegerConstructor} 'Integer'
    | {infer FloatConstructor} 'Float'
    | {infer StringConstructor} 'String'
    | {infer BooleanConstructor} 'Boolean'
    ;

Direction returns string: 'in' | 'out';

hidden terminal WS: /\s+/;
terminal LOWER_ID: /[a-z][\w_]*/;
terminal UPPER_ID: /[_A-Z][\w_]*/;
terminal INT: /0|[1-9][0-9]*/;
terminal STRING: /"[^"]*"|'[^']*'/;

hidden terminal ML_COMMENT: /\/\*[\s\S]*?\*\//;
hidden terminal SL_COMMENT: /\/\/[^\n\r]*/;
